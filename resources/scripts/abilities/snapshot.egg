class SnapshotAbility inherits Ability {
	name = "Snapshot";
	icon = "snapshot.png";

	function use(%this, %target) {
		Parent::use(%this, %target);

		if(%this.roll(%target)) {
			%target.addHealth(-5);
		}
		else {
			%target.showMiss();
		}

		playSound("SnapshotFire");
	}

	function accuracy(%this, %target) {
		if(!%target) {
			return 0;
		}
		
		%range = 20;
		%accuracy = (-1 / %range) * distance(%target.getPosition(), %this.owner.getPosition()) + 1;
		%accuracy *= getCoverAccuracyMod(%target.inCover(%this.owner));
		if(%accuracy < 0.03) {
			return 0.03;
		}
		return %accuracy;
	}

	function cost(%this, %target) {
		return 1;
	}

	function usable(%this, %target) {
		return %target != null && %target.inCover(%this.owner) != $Cover::Full && Parent::usable(%this, %target);
	}

	function description(%this, %target) {
		%pips = %this.getAPPips(1);

		%accuracy = "";
		if(%this.usable(%target)) {
			%accuracy = "<tr><td>Accuracy:" SPC mFloor(%this.accuracy(%target) * 100) @ "%</td></tr>";
		}
		else if(isObject(%target)) {
			%accuracy = "<tr><td>Accuracy: 0%</td></tr>";
		}

		%table = "<table class=\"ability-stats-table\">" @ %accuracy @ "<tr><td>Damage: 5</td></tr><tr><td>" @ %pips @ "</td></tr></table>";
		%html = "<div>A quick, inaccurate attack against an enemy</div>";
		return %html @ %table;
	}

	function actionName(%this, %target) {
		return "Fire";
	}
};
