class OverwatchAbility inherits Ability;

function OverwatchAbility::onAdd(%this) {
	Parent::onAdd(%this);

	%this.name = "Overwatch";
	%this.icon = "overwatch.png";
}

function OverwatchAbility::overwatch(%this, %target) {
	%this.owner.overwatch = false;

	if(%this.roll(%target)) {
		%target.addHealth(-2);
	}

	playSound("SnapshotFire");
}

function OverwatchAbility::use(%this, %target) {
	Parent::use(%this, %target);
	%this.owner.overwatch = true;

	playSound("OverwatchUse");
}

function OverwatchAbility::accuracy(%this, %target) {
	if(!%target) {
		return 0;
	}
	
	%range = 20;
	%accuracy = (-1 / mPow(%range, 0.7)) * mPow(distance(%target.getPosition(), %this.owner.getPosition()), 0.7) + 1;
	%accuracy *= getCoverAccuracyMod(%target.inCover(%this.owner));
	if(%accuracy < 0.03) {
		return 0.03;
	}
	return %accuracy;
}

function OverwatchAbility::cost(%this, %target) {
	%ap = %this.owner.ap;
	if(%ap == 0) {
		return %ap = 1;
	}
	return %ap;
}

function OverwatchAbility::usable(%this, %target) {
	%result = Parent::usable(%this, %target);
	return %result;
}

function OverwatchAbility::description(%this, %target) {
	return "<div>Take a shot at an enemy after they have finished moving.</div>";
}

function OverwatchAbility::actionName(%this, %target) {
	return "Overwatch";
}
