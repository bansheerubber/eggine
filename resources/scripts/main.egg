exec("ability.egg");
exec("tile.egg");
exec("abilities/snapshot.egg");
exec("abilities/trainedshot.egg");
exec("abilities/overwatch.egg");
exec("team.egg");
exec("unit.egg");
exec("editor.egg");
exec("targetting.egg");
exec("turn.egg");

function r() {
	hotReload();
}

function init() {
	// unit stuff
	$Unit::HoveredTile = null;

	$Unit::TargetSprite = null;
	$Unit::SelectedTarget = null;

	$Unit::SelectedAbility = null; // try to use the same ability when we cycle through units
	$Unit::SprintBorderColor = {0; 0; 1; 1};

	schedule(100, "teamInit");

	// tile stuff
	$Tile::Directions = new Array();
	$Tile::Directions.push(
		{0; -1; 0}, // north
		{1; 0; 0}, // east
		{0; 1; 0}, // south
		{-1; 0; 0} // west
	);

	// editor stuff
	$Editor::Enabled = false;

	$Editor::GhostTile = null;
	$Editor::GhostLine = new Array();
	$Editor::MouseDown = false;

	$Editor::LastStart = null;
	$Editor::LastEnd = null;
	$Editor::LastDirection = null;
	$Editor::LastAxis  = null;

	// cover enums
	$Cover::None = 0;
	$Cover::HalfPartial = 1;
	$Cover::Half = 2;
	$Cover::Full = 3;
	
	addKeybind("chunk.rotateClockwise", "q", "onRotateClockwise");
	addKeybind("chunk.rotateClockwise", "d-pad-left", "onRotateClockwise");

	addKeybind("chunk.rotateCounterClockwise", "e", "onRotateCounterClockwise");
	addKeybind("chunk.rotateCounterClockwise", "d-pad-right", "onRotateCounterClockwise");

	addKeybind("unit.gamepadPressTile", "b-button", "onGamepadPressTile");

	addKeybind("unit.gamepadUseAbility", "a-button", "onGamepadUseAbility");

	addKeybind("unit.nextAbility", "y-button", "onNextAbility");

	addKeybind("unit.previousAbility", "x-button", "onPreviousAbility");

	addKeybind("editor.mouseDown", "left-mouse-button", "onEditorMouseDown");	
}

function HTMLElement::onClick(%this) {
	
}

function distance(%a, %b) {
	return mSqrt(mPow(%a[0] - %b[0], 2) + mPow(%a[1] - %b[1], 2) + mPow(%a[2] - %b[2], 2));
}

function isTileSelectable(%position) {
	%character = getChunkContainer().getCharacter(%position + {0; 0; 1});
	if(isObject(%character)) {
		return %character;
	}
	return 0;
}

function onRotateClockwise(%state) {
	if(%state) {
		getChunkContainer().setRotation((getChunkContainer().getRotation() + 1) % 4);
	}
}

function onRotateCounterClockwise(%state) {
	if(%state) {
		%rotation = getChunkContainer().getRotation() - 1;
		if(%rotation < 0) {
			%rotation += 4;
		}
		getChunkContainer().setRotation(%rotation);
	}
}

function onGamepadPressTile(%state) {
	if(!%state) {
		return;
	}

	%position = $Unit::HoveredTile;
	if(!isTileSelectable(%position)) {
		%selectedCharacter = getChunkContainer().getSelectedCharacter();
		if(isObject(%selectedCharacter)) {
			%selectedCharacter.move(%position);
		}
	}
}

function onGamepadUseAbility(%state) {
	if(!%state) {
		return;
	}

	%selectedCharacter = getChunkContainer().getSelectedCharacter();
	if(isObject(%selectedCharacter) && %selectedCharacter.selectedAbility.usable($Unit::SelectedTarget)) {
		%selectedCharacter.selectedAbility.use($Unit::SelectedTarget);

		if(!isObject($Unit::SelectedTarget)) {
			selectTarget(null);
		}
	}
}

function onNextAbility(%state) {
	if(!%state) {
		return;
	}

	%character = getChunkContainer().getSelectedCharacter();
	if(%character) {
		%character.selectNextAbility();
		playSound("Flip");
	}
}

function onPreviousAbility(%state) {
	if(!%state) {
		return;
	}

	%character = getChunkContainer().getSelectedCharacter();
	if(%character) {
		%character.selectPreviousAbility();
		playSound("Flip");
	}
}

function onSelectTile(%position, %browsing) {
	if(%browsing) {
		$Unit::HoveredTile = %position;
		%character = getChunkContainer().getSelectedCharacter();
		if(isObject(%character)) {
			%path = %character.getPath(%position);
			%character.path = %path;
			if(isObject(%path)) {
				%path.showDots();
			}
		}
	}
	else {
		%character = isTileSelectable(%position);
		if(isObject(%character)) {
			if(getChunkContainer().getPlayerTeam().has(%character)) {
				getChunkContainer().selectCharacter(%character);
				playSound("SelectUnit");
			}
			else if(getChunkContainer().getEnemyTeam().has(%character)) {
				selectTarget(%character);
				playSound("SelectEnemy");
			}
		}
	}
}

function onRightClickTile(%position) {
	if(isObject(getChunkContainer().getSelectedCharacter())) {
		%character = getChunkContainer().getSelectedCharacter();
		%character.move(%position);
	}
}
