class SwitchTeamElement inherits HTMLElement {
	function onClick(%this) {
		getChunkContainer().selectCharacter(null);
		selectTarget(null);
		
		if($Team::Player == getChunkContainer().getPlayerTeam()) {
			getChunkContainer().setPlayerTeam($Team::Enemy);
			getChunkContainer().setEnemyTeam($Team::Player);
		}
		else {
			getChunkContainer().setPlayerTeam($Team::Player);
			getChunkContainer().setEnemyTeam($Team::Enemy);
		}

		// go through current team and reset every ability
		for(%i = 0; %i < getChunkContainer().getPlayerTeam().size(); %i++) {
			%abilities = getChunkContainer().getPlayerTeam().get(%i).abilities;
			for(%j = 0; %j < %abilities.size(); %j++) {
				if(%abilities[%j].isMethod("onStartTurn")) {
					%abilities[%j].onStartTurn();
				}
			}
		}

		getHTMLElementById("team-name").clear();
		getHTMLElementById("team-name").createChild("Team:" SPC getChunkContainer().getPlayerTeam().getName());
	}
};

function Team::selectNextCharacter(%this) {
	%this.selectedIndex = (%this.selectedIndex + 1) % %this.size();
	
	%character = %this.get(%this.selectedIndex);
	getChunkContainer().selectCharacter(%character);
	getActiveCamera().pan(getActiveCamera().getPosition(), tileToScreen(%character.getPosition()), 0.2);

	playSound("SwapUnit");
}

function Team::selectPreviousCharacter(%this) {
	%this.selectedIndex--;
	if(%this.selectedIndex < 0) {
		%this.selectedIndex = %this.size() - 1;
	}

	%character = %this.get(%this.selectedIndex);
	getChunkContainer().selectCharacter(%character);
	getActiveCamera().pan(getActiveCamera().getPosition(), tileToScreen(%character.getPosition()), 0.2);

	playSound("SwapUnit");
}

function selectNextCharacter(%state) {
	if(!%state) {
		return;
	}

	getChunkContainer().getPlayerTeam().selectNextCharacter();
}

function selectPreviousCharacter(%state) {
	if(!%state) {
		return;
	}

	getChunkContainer().getPlayerTeam().selectPreviousCharacter();
}

function teamInit() {
	addKeybind("team.selectPreviousCharacter", "[", "selectPreviousCharacter");
	addKeybind("team.selectNextCharacter", "]", "selectNextCharacter");
	addKeybind("team.selectPreviousCharacter", "left-button", "selectPreviousCharacter");
	addKeybind("team.selectNextCharacter", "right-button", "selectNextCharacter");

	$Team::Player = getChunkContainer().getPlayerTeam();
	$Team::Enemy = getChunkContainer().getEnemyTeam();
}
