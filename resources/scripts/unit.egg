function Unit::onAdd(%this) {
	%this.abilities = new Array();

	%ability1 = new SnapshotAbility();
	%ability1.owner = %this;
	%this.abilities.push(%ability1);

	%ability2 = new TrainedShotAbility();
	%ability2.owner = %this;
	%this.abilities.push(%ability2);

	%ability3 = new OverwatchAbility();
	%ability3.owner = %this;
	%this.abilities.push(%ability3);

	%this.maxMoves = 5;
	%this.maxSprintMoves = 15;
	%this.moves = 5;
	%this.sprintMoves = 15;

	%this.ap = 2;

	%this.setMaxHealth(getRandom(3, 8));
	%this.setHealth(getRandom(2, %this.getMaxHealth() - 1));
}

function Unit::coverList(%this) {
	%array = new Array();

	// if our unit is on a wall tile
	if(Tile::isWall(%this.getPosition())) {
		%map = new Map();
		%map["position"] = %this.getPosition();
		%map["vector"] = Tile::wallAlong(%position);
		%map["isWall"] = true;
		%array.push(%map);
		return %array;
	}

	for(%i = 0; %i < 4; %i++) {
		%direction = $Tile::Directions[%i];
		%position = %direction + %this.getPosition();
		if(Tile::isTile(%position) && !Tile::isWall(%position)) {
			%map = new Map();
			%map["position"] = %position;
			%map["vector"] = $Tile::Directions[(%i + 1) % 4]; // rotate vector by 90 degrees to get "along the wall" vector
			%map["isWall"] = Tile::isWall(%map["position"]);
			%array.push(%map);
		}
	}
	return %array;
}

// return 2 if behind impenetrable cover, return 1 if behind cover but can still be shot, return 0 if no cover at all
function Unit::inCover(%this, %attacker) {
	%result = raycast(%attacker.getPosition(), %this.getPosition());
	if(%result == null) {
		return $Cover::None;
	}

	// test the hit tile against our cover list to see if they have line of sight
	%coverList = %this.coverList();
	if(%coverList.size() == 0) { // TODO do additional tests to see if a half-height wall is obscuring us/etc
		return $Cover::Full;
	}

	%hitCover = null;
	for(%i = 0; %i < %coverList.size(); %i++) {
		%cover = %coverList[%i];
		if(Tile::wallTest(%result, %cover["position"], %cover["vector"])) {
			%hitCover = %cover;
			break;
		}
	}

	if(%hitCover == null) { // TODO is this right?
		return $Cover::None;
	}

	// wall test: if our unit is on the edge of a wall, do some tests to see if we're still hit by the enemy. an enemy that can see us at an angle
	// because we're at the edge of a wall can shoot at us and hit
	if(%hitCover["isWall"]) {
		// if the attacker hits a wall that isn't our cover, then the attacker can't hit us https://bansheerubber.com/i/f/c3cks.png
		echo(%result NL %hitCover["position"] NL %hitCover["vector"]);
		if(%result != %hitCover["position"] && Tile::isWall(%result)) {
			return $Cover::Full;
		}

		// get the tiles adjacent to our cover
		%attackerPosition = %attacker.getPosition();
		%adjacentLeft = %hitCover["position"] + %hitCover["vector"];
		%adjacentRight = %hitCover["position"] - %hitCover["vector"];
		%adjacentLeftDistance = distance(%adjacentLeft, %attackerPosition);
		%adjacentRightDistance = distance(%adjacentRight, %attackerPosition);

		if(%adjacentLeftDistance == %adjacentRightDistance) { // if the attacker is directly facing our cover, then they can't hit us
			return $Cover::Full;
		}
		// next two if statements test for this: https://bansheerubber.com/i/f/w0Iqt.png
		else if(%adjacentLeftDistance < %adjacentRightDistance && Tile::isWall(%adjacentLeft)) {
			return $Cover::Full;
		}
		else if(%adjacentLeftDistance > %adjacentRightDistance && Tile::isWall(%adjacentRight)) {
			return $Cover::Full;
		}
		else {
			return $Cover::FullPartial;
		}
	}

	// if our cover is not a wall, then the attacker can hit us but with the half-height cover modifier 
	return $Cover::Half;
}

function getCoverAccuracyMod(%cover) {
	if(%cover == $Cover::None) {
		return 1;
	}
	else if(%cover == $Cover::Half) {
		return 1 / 1.5;
	}
	else if(%cover == $Cover::FullPartial) {
		return 1 / 2;
	}
	else if(%cover == $Cover::Full) {
		return 0;
	}
}

function Unit::updateUI(%this) { // if we're selected, then update the UI
	if(getChunkContainer().getSelectedCharacter() == %this) {
		%this.getDestinations().showBorder();
		%this.getSprintDestinations().showBorder($Unit::SprintBorderColor);
		%this.updateTargettingUI();
		%this.updateAbilityList();
	}
}

function Unit::onSelect(%this) {
	%this.updateUI();
}

function Unit::onDeSelect(%this) {
	%this.getDestinations().hideBorder();
	%this.getSprintDestinations().hideBorder();
	%this.path = %this.getPath($Unit::HoveredTile);
	%this.path.hideBorder();
}

function Unit::addAP(%this, %ap) {
	%this.setAP(%this.ap + %ap);
}

function Unit::setAP(%this, %ap) {
	if(%ap >= 2) {
		%this.ap = 2;
		%this.moves = %this.maxMoves;
		%this.sprintMoves = %this.maxSprintMoves;
	}
	else if(%ap == 1) {
		%this.ap = 1;
		%this.moves = 0;
		%this.sprintMoves = %this.maxSprintMoves - %this.maxMoves;
	}
	else { // 0 or negative ap
		%this.sprintMoves = 0;
		%this.moves = 0;
		%this.ap = 0;
	}

	%this.updateUI();
}

function Unit::move(%this, %position) {
	%unitPosition = %this.getPosition();
	%distance = (%this.path.size() - 1);

	if(!isObject(%this.path)) {
		return;
	}

	if(%this.getDestinations().has(%position) && %this.moves >= %distance) {
		%this.addAP(-1);
		%this.setPosition(%position + {0; 0; 1});
	}
	else if(%this.getSprintDestinations().has(%position) && %this.sprintMoves >= %distance) {
		%this.addAP(-2);
		%this.setPosition(%position + {0; 0; 1});
	}
}

function Unit::updateTargettingUI(%this) {
	getHTMLElementById("enemy-icons").clear();
	%html = "";
	%team = getChunkContainer().getEnemyTeam();
	for(%i = 0; %i < %team.size(); %i++) {
		%enemy = %team.get(%i);
		// check if any of our abilities are usable against the enemy
		%usable = false;
		for(%j = 0; %j < %this.abilities.size(); %j++) {
			%ability = %this.abilities[%j];
			if(!%ability.usable(%enemy)) {
				continue;
			}
			%usable = true;
		}

		if(%usable) {
			%html = %html @ "<img enemy-id=\"" @ %i @ "\" es-class=\"EnemyIconElement\" class=\"enemy-icon\" src=\"/images/alien.png\" />";
		}
	}
	getHTMLElementById("enemy-icons").createChild(%html);
}

function Unit::updateAbilityList(%this) {
	getHTMLElementById("player-abilities").clear();
	%this.shownAbilities = 0;
	for(%i = 0; %i < %this.abilities.size(); %i++) {
		%ability = %this.abilities[%i];
		%disabled = %ability.usable($Unit::SelectedTarget) ? "" : "disabled";
		getHTMLElementById("player-abilities").createChild("<img es-class=\"AbilityElement\" ability-index=\"" @ %i @ "\" class=\"ability-icon " @ %disabled @ "\" src=\"/images/" @ %ability.icon @ "\" />");

		if(%ability.name == $Unit::SelectedAbility.name) {
			%this.selectAbility(%i); // try to select same ability on same target between units
			%selected = true;
		}

		%this.shownAbilities++;
	}

	if(!%selected) { // if we couldn't select our selected ability, then it is missing from the HTML ability list and we should hide the ability UI
		%this.selectAbility(-1);
	}
}

function Unit::selectAbility(%this, %index) {
	%name = getHTMLElementById("ability-description-name");
	%name.clear();
	%description = getHTMLElementById("ability-description-body");
	%description.clear();
	%button = getHTMLElementById("ability-description-button");
	%button.clear();

	if(%index == -1) {
		%this.selectedAbility = null;
		getHTMLElementById("ability-description").setStyleAttribute("display", "none");
		return;
	}
	getHTMLElementById("ability-description").setStyleAttribute("display", "block");

	if(%this.abilities[%index]) {
		%ability = %this.abilities[%index];
		echo(%index SPC %ability);
		%name.createChild(%ability.name);
		%description.createChild(%ability.description($Unit::SelectedTarget));

		if(%ability.usable($Unit::SelectedTarget)) {
			%button.createChild(%ability.actionName($Unit::SelectedTarget));
			%button.setStyleAttribute("display", "inline-block");
		}
		else {
			%button.setStyleAttribute("display", "none");
		}

		%this.selectedAbility = %ability;
		$Unit::SelectedAbility = %ability;
	}
	else {
		%this.selectedAbility = null;
	}
}

function Unit::selectNextAbility(%this) {
	if(%this.shownAbilities == 0) {
		return;
	}

	if(%this.selectedAbility == null) {
		%this.selectAbility(0);
		return;
	}
	
	for(%i = 0; %i < %this.abilities.size(); %i++) {
		if(%this.selectedAbility == %this.abilities[%i]) {
			%index = %i;
			break;
		}
	}

	%next = (%index + 1) % %this.abilities.size();
	%this.selectAbility(%next);
}

function Unit::selectPreviousAbility(%this) {
	if(%this.shownAbilities == 0) {
		return;
	}

	if(%this.selectedAbility == null) {
		%this.selectAbility(0);
		return;
	}
	
	for(%i = 0; %i < %this.abilities.size(); %i++) {
		if(%this.selectedAbility == %this.abilities[%i]) {
			%index = %i;
			break;
		}
	}

	%prev = (%index - 1);
	if(%prev < 0) {
		%prev = %this.abilities.size() - 1;
	}
	%this.selectAbility(%prev);
}

function Unit::triggerOverwatch(%this, %target) {
	for(%i = 0; %i < %this.abilities.size(); %i++) {
		if(%this.abilities[%i].name == "Overwatch") {
			 %this.abilities[%i].overwatch(%target);
		}
	}
}

function fireOverwatch(%target) {
	for(%i = 0; %i < getTeamCount(); %i++) {
		%team = getTeam(%i);
		if(%target.getTeam() == %team) {
			continue;
		}

		for(%j = 0; %j < %team.size(); %j++) {
			%guy = %team.get(%j);
			if(%guy.overwatch) {
				%guy.triggerOverwatch(%target);
			}
		}
	}
}

package unit {
	function Unit::setPosition(%this, %position) {
		Parent::setPosition(%this, %position);

		if(%this == getChunkContainer().getSelectedCharacter()) {
			%this.updateUI();
			%this.path = %this.getPath($Unit::HoveredTile);
			if(%this.path) {
				%this.path.showDots();
			}
		}

		fireOverwatch(%this);
	}

	function ChunkContainer::selectCharacter(%this, %character) {
		%test = getChunkContainer().getSelectedCharacter();
		if(isObject(%test)) {
			%test.onDeSelect();
		}
		
		Parent::selectCharacter(%this, %character);

		%character.onSelect();
	}
};
